plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'jacoco'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.what3words.testing"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.1"

        buildConfigField 'String', 'W3W_PRE_PROD_URL', findProperty('W3W_PRE_PROD_URL') ? "\"${findProperty('W3W_PRE_PROD_URL')}\"" : "\"PRE_PRO_URL_HERE\""
        buildConfigField 'String', 'W3W_API_KEY', findProperty('PRE_PROD_API_KEY') ? "\"${findProperty('PRE_PROD_API_KEY')}\"" : "\"YOUR_API_KEY_HERE\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

       // testInstrumentationRunnerArguments clearPackageData: 'true'
       // testInstrumentationRunnerArguments useTestStorageService: 'true'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
        unitTests.returnDefaultValues = true
    }

    jacoco {
        version = "$jacoco_version"
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'com.what3words.testing'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.core:core-ktx:$androidx_core_ktx_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraint_layout_version"
    implementation "com.google.android.material:material:$google_material_version"
    implementation "com.what3words:w3w-android-wrapper:$what3words_android_wrapper_version"
    implementation project(":lib")

    // testing
    androidTestImplementation "androidx.test.ext:junit:$androidx_junit_version"
    implementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    debugImplementation "androidx.test:rules:$androidx_test_version"
    debugImplementation "androidx.test.ext:junit:$androidx_junit_version"
    implementation "androidx.test.espresso:espresso-core:$espresso_version"
    debugImplementation "androidx.test:runner:$androidx_test_runner_version"
    androidTestUtil "androidx.test:orchestrator:$androidx_test_orchestrator_version"
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['createDebugCoverageReport']) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Debug build"

    reports {
        xml.required = true
        html.required = true
    }

    def fileFilter = ['android/databinding/**/*.class',
                      '**/*databinding.*',
                      '**/android/databinding/*',
                      '**/androidx/databinding/*',
                      '**/BR.*',
                      '**/R.class',
                      '**/R$*.class',
                      '**/BuildConfig.*',
                      '**/Manifest*.*',
                      '**/*Test*.*',
                      'android/**/*.*',
                      'androidx/**/*.*',
                      'autosuggestsample/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ]))

}