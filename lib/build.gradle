plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.dokka' version '1.5.0'
}

def libVersion = findProperty('LIBRARY_VERSION') ? findProperty('LIBRARY_VERSION') : "1.0-debug"

apply from: 'maven-push.gradle'
apply from: '../jacoco.gradle'

android {
    compileSdkVersion 32
    buildToolsVersion libs.versions.buildToolsVersion.get()

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 32
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        buildConfigField("String", "VERSION_NAME", "\"${libVersion}\"")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        debug {
            testCoverageEnabled = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    tasks.dokkaGfm.configure {
        suppressObviousFunctions.set(true)
        suppressInheritedMembers.set(true)
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.3.1"
    }
}

dependencies {
    api libs.androidx.recyclerview
    implementation libs.androidx.core

    // kotlin
    implementation libs.jetbrains.kotlinx.coroutines.core
    implementation libs.jetbrains.kotlinx.coroutines.android
    testImplementation libs.jetbrains.kotlinx.coroutines.test

    testImplementation testLibs.junit
    testImplementation testLibs.androidx.test.core
    testImplementation testLibs.google.truth
    androidTestImplementation testLibs.androidx.junit
    androidTestImplementation testLibs.androidx.espresso.core
    testImplementation testLibs.mockk
    testImplementation libs.json
    testImplementation testLibs.androidx.arch.core

    // what3words wrapper
    api libs.what3words.android.wrapper

    implementation libs.google.guava
    implementation libs.nishkarsh.android.permissions
    implementation libs.androidx.constraint.layout
    implementation libs.airbnb.lottie

    // compose
    implementation libs.compose.foundation
    compileOnly libs.compose.constraint.layout
    compileOnly libs.compose.ui
}