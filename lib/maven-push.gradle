def ossrhUsername = findProperty('OSSRH_USERNAME')
def ossrhPassword = findProperty('OSSRH_PASSWORD')
def signingKey = findProperty('SIGNING_KEY')
def signingKeyPwd = findProperty('SIGNING_KEY_PWD')
def libVersion = "3.2.1"

task androidSourcesJar(type: Jar) {
    archiveClassifier.convention('sources')
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.convention('javadoc')
    archiveClassifier.set('javadoc')
    from "$buildDir/dokka"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                // You can then customize attributes of the publication as shown below.
                groupId = "com.what3words"
                artifactId = "w3w-android-components"
                version = libVersion

                artifact androidSourcesJar
                artifact androidJavadocsJar

                pom {
                    name = "w3w-android-components"
                    description = 'Android library for what3words REST API.'
                    url = 'https://github.com/what3words/w3w-android-components'
                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'https://github.com/what3words/w3w-android-components/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'what3words'
                            name = 'what3words'
                            email = 'development@what3words.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/what3words/w3w-android-components.git'
                        developerConnection = 'scm:git:ssh://git@github.com:what3words/w3w-android-components.git'
                        url = 'https://github.com/what3words/w3w-android-components'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "sonatype"

                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(signingKey, signingKeyPwd)
    sign publishing.publications
}